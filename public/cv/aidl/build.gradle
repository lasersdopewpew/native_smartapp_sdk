plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}
android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        buildConfigField 'String', 'CV_API_VERSION', "\"3.0.0\""
        versionName "3.0.0-rc1"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':public:cv:entity')

    implementation "androidx.annotation:annotation:$versions.annotation"
}

afterEvaluate {
    publishing {
        repositories.add(rootProject.repositories.getByName('OSSRH'))

        publications {
            mavenCvAidl(MavenPublication) {
                from components.release

                groupId = rootProject.ext.pomGroupID + '.cvapi'
                artifactId = "aidl"
                version = android.defaultConfig.versionName

                pom {
                    name = "cv:aidl"
                    description = 'aidl interface for communication with service'
                    url = publication.githubUrl
                    licenses {
                        license {
                            name = publication.licenseName
                            url = publication.licenseUrl
                        }
                    }
                    developers {
                        developer {
                            name = 'Dmitry Levshunov'
                            email = 'Levshunov.D.A@sberbank.ru'
                        }
                        developer {
                            name = 'Irina Karpenko'
                            email = 'IViKarpenko@sberbank.ru'
                        }
                    }
                    scm {
                        connection = publication.connectionUrl
                        developerConnection = publication.connectionUrl
                        url = publication.githubUrl
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenCvAidl
    }
}
