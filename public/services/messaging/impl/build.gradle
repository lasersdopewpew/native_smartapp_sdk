plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

dependencies {
    api project(':public:services:messaging:api')
    implementation project(':public:services:messaging:aidl')

    implementation project(':public:common:binderhelper')
    implementation project(':public:common:logger')

    implementation "androidx.annotation:annotation:1.2.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.3"
}

android {
    compileSdkVersion 29
    buildToolsVersion  "29.0.2"

    buildTypes {
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

//task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
//    archiveClassifier.set('javadoc')
//    from androidJavadocs.destinationDir
//}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

            publications {
                mavenMessagingImpl(MavenPublication) {
                    from components.release

//                    artifact androidJavadocsJar TODO fix
                    artifact androidSourcesJar

                    groupId rootProject.ext.pomGroupID+'.messaging'
                    artifactId "impl"
                    version rootProject.ext.pomVersion

                    pom {
                        name = "messaging:impl"
                        description = 'messaging libray serves to connect native apk with smartapp backend'
                        url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        licenses {
                            license {
                                name = 'Sber Public License at-nc-sa v.2'
                                url = 'https://github.com/sberdevices/native_smartapp_sdk/blob/master/LICENSE.txt'
                            }
                        }
                        developers {
                            developer {
                                name = 'Igor Perminov'
                                email = 'Perminov.I.Yurye@sberbank.ru'
                            }
                            developer {
                                name = 'Nikolay Pahomov'
                                email = 'NMPakhomov@sberbank.ru'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/sberdevices/native_smartapp_sdk.git'
                            developerConnection = 'scm:git:ssh://github.com:sberdevices/native_smartapp_sdk.git'
                            url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        }
                    }

                    versionMapping {
                        allVariants {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenMessagingImpl
    }
}