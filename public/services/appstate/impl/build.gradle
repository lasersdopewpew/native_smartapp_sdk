plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

dependencies {
    api project(':public:services:appstate:api')
    implementation project(':public:services:appstate:aidl')

    implementation project(':public:common:asserts')
    implementation project(':public:common:logger')

    implementation "androidx.core:core-ktx:$versions.androidx.core"
    implementation "androidx.annotation:annotation:$versions.annotation"
}

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

        buildTypes {
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    android.libraryVariants.all { variant ->
//        if (variant.name == 'release') {
//            owner.classpath += variant.javaCompileProvider.get().classpath
//        }
//    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = publication.deployUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

            publications {
                mavenAppstateImpl(MavenPublication) {
                    from components.release

                    artifact androidJavadocsJar
                    artifact androidSourcesJar

                    groupId rootProject.ext.pomGroupID+'.appstate'
                    artifactId "impl"
                    version rootProject.ext.pomVersion

                    pom {
                        name = "appstate"
                        description = 'appstate is a library used to transmit native app state to smartapp backend'
                        url = publication.githubUrl
                        licenses {
                            license {
                                name = publication.licenseName
                                url = publication.licenseUrl
                            }
                        }
                        developers {
                            developer {
                                name = 'Igor Perminov'
                                email = 'Perminov.I.Yurye@sberbank.ru'
                            }
                            developer {
                                name = 'Nikolay Pahomov'
                                email = 'NMPakhomov@sberbank.ru'
                            }
                        }
                        scm {
                            connection = publication.connectionUrl
                            developerConnection = publication.connectionUrl
                            url = publication.githubUrl
                        }
                    }

                    versionMapping {
                        allVariants {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenAppstateImpl
    }
}