apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

dependencies {
    api project(':public:services:appstate:api')
    implementation project(':public:services:appstate:aidl')

    implementation project(':public:common:asserts')
    implementation project(':public:common:logger')

    implementation 'androidx.core:core:1.3.2'
    implementation "androidx.annotation:annotation:1.3.0-alpha01"
}

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 29
    buildToolsVersion  "29.0.2"

        buildTypes {
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    android.libraryVariants.all { variant ->
//        if (variant.name == 'release') {
//            owner.classpath += variant.javaCompileProvider.get().classpath
//        }
//    }
    exclude '**/R.html', '**/R.*.html', '**/index.html'
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }

            publications {
                mavenAppstateImpl(MavenPublication) {
                    from components.release

                    artifact androidJavadocsJar
                    artifact androidSourcesJar

                    groupId rootProject.ext.pomGroupID+'.appstate'
                    artifactId "impl"
                    version rootProject.ext.pomVersion

                    pom {
                        name = "appstate"
                        description = 'appstate is a library used to transmit native app state to smartapp backend'
                        url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        licenses {
                            license {
                                name = 'Sber Public License at-nc-sa v.2'
                                url = 'https://github.com/sberdevices/native_smartapp_sdk/blob/master/LICENSE.txt'
                            }
                        }
                        developers {
                            developer {
                                name = 'Igor Perminov'
                                email = 'Perminov.I.Yurye@sberbank.ru'
                            }
                            developer {
                                name = 'Nikolay Pahomov'
                                email = 'NMPakhomov@sberbank.ru'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/sberdevices/native_smartapp_sdk.git'
                            developerConnection = 'scm:git:ssh://github.com:sberdevices/native_smartapp_sdk.git'
                            url = 'https://github.com/sberdevices/native_smartapp_sdk'
                        }
                    }

                    versionMapping {
                        allVariants {
                            fromResolutionResult()
                        }
                    }
                }
            }
        }
    }
}

afterEvaluate {
    signing {
        sign publishing.publications.mavenAppstateImpl
    }
}